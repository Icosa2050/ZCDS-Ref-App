managed implementation in class zbp_r_salesordertp unique;
strict ( 2 );
//with draft;
//extensible { with additional save; with determinations on modify; with determinations on save; with validations on save; }

// Allow consumers to use PRIVILEGED mode
//cl_abap_behv_aux=>get_current_context(    IMPORTING privileged = data(privileged)  ).

with privileged mode disabling NoCheckWhenPrivileged;


define own authorization context by privileged mode and { }

define authorization context NoCheckWhenPrivileged { 'Z_VBAK_VK'; }

define authorization context NoCheckWhenReadingOrModifying
for disable ( modify, read ) ##WARN_OK { }

define behavior for ZR_SalesOrderTP alias SalesOrder
persistent table zsalesorder
//with additional save
//lock master unmanaged total etag LastChangeDateTime
lock master
etag master LastChangeDateTime

authorization master ( global, instance )
//etag master <field_name>
{
  create ( features : global );
  update ( features : instance, precheck );
  delete;
  association _Item { create ( features : instance ); }
  field ( mandatory : create, readonly : update ) SalesOrder;
  field ( readonly ) CreatedByUser;
  field ( readonly ) CreationDateTime;
  field ( readonly ) LastChangedByUser;
  field ( readonly ) LastChangeDateTime;
  field ( readonly ) DeliveryCompleted;
  field ( mandatory ) SalesOrderType;
  field ( mandatory ) SalesOrganization;
  field ( mandatory ) SoldToParty;

  action ( features : instance ) Delete;
  static factory action ( features : global ) CreateFromQuote deep parameter ZD_SalesOrderCreateFromQuoteP [1];

  function GetNumberOfItems deep result [1] ZD_SalesOrderGetNumberofItemsR;
  function GetSalesOrder deep result [1] ZD_SalesOrderGetResult;

  determination CalculateTotalNetAmount on modify { field NetAmount, TransactionCurrency; }
  validation VerifySoldToParty on save { field SoldToParty; }
  determine action Check
  {
    validation ( always ) VerifySoldToParty;
    validation ( always ) SalesOrderItem~VerifyProduct;
    validation ( always ) SalesOrderScheduleLine~VerifyQuantityUnit;
  }

}

define behavior for ZR_SalesOrderItemTP alias SalesOrderItem
early numbering
persistent table zsalesorderitem
lock dependent by _SalesOrder
authorization dependent by _SalesOrder
//etag master LastChangeDateTime
{

  action delete;
  update ( features : instance );
  delete ( features : instance );
  field ( readonly ) SalesOrder;
  field ( readonly : update ) SalesOrderItem;
  field ( readonly ) LastChangedByUser;
  field ( readonly ) LastChangeDateTime;


  association _SalesOrder;
  association _ScheduleLine { create ( features : instance ); }
  unmanaged association _RequestedScheduleLine
  { create ( authorization : update, features : instance ); }
  unmanaged association _ConfirmedScheduleLine
  { create ( authorization : update, features : instance ); }

  action ( authorization : update, features : instance ) Copy
    parameter ZD_SALESORDERITEMCOPYPARAMETER
    result [1..*] $self;

  determination CalculateTotalAmount on save { field Product, OrderQuantity, OrderQuantityUnit; }
  determination CalculateNetAmount on modify { field NetAmount, TransactionCurrency; }
  validation VerifyProduct on save { field Product; }

}

define behavior for ZR_SalesOrderScheduleLineTP alias SalesOrderScheduleLine
early numbering
persistent table zsalesordersline
lock dependent by _SalesOrder
authorization dependent by _SalesOrder
//etag master <field_name>
{
  update (features: instance );
  delete (features: instance );
  field ( readonly ) SalesOrder;
  field ( readonly : update ) SalesOrderItem;
  field ( readonly : update ) SalesOrderScheduleLine;
  association _SalesOrder;
  association _SalesOrderItem;
  validation VerifyQuantityUnit on save { field OrderQuantityUnit; }
}